include: "rules/common.smk"


if config.get("load_fair_genome_indexer", True):

    include: "rules/fair_genome_indexer.smk"


include: "rules/fastq_info.smk"
include: "rules/fastq_screen.smk"
include: "rules/fastq_utils.smk"
include: "rules/fastqc.smk"
include: "rules/librarian.smk"
include: "rules/multiqc.smk"
include: "rules/prepare_input.smk"
include: "rules/samples_stats.smk"
include: "rules/seqkit.smk"
include: "rules/seqtk.smk"


rule fair_fastqc_multiqc_target:
    input:
        samples_stats=expand(
            "tmp/fair_fastqc_multiqc_samples_stats/{sample.sample_id}.{sample.species}.{sample.build}.{sample.release}.dna.statistics.yaml",
            sample=samples.itertuples(),
        ),
        multiqc=[
            "results/QC/MultiQC_FastQC.html",
            "results/QC/MultiQC_FastQC_data.zip",
        ],
        fastqinfo_pe=expand(
            "results/QC/fastqinfo/{sample.sample_id}.{sample.species}.{sample.build}.{sample.release}.{datatype}.pe.tsv",
            sample=get_pair_ended_samples(),
            datatype=("dna", "transcripts"),
        ),
        seqkit_stats="results/QC/SeqKit.Stats.csv",
        fastqutils_info_pe=expand(
            "results/QC/fastq_info/{sample.sample_id}.pe.txt",
            sample=get_pair_ended_samples(),
        ),
        fastqutils_info_se=expand(
            "results/QC/fastq_info/{sample.sample_id}.se.txt",
            sample=get_single_ended_samples(),
        ),
        seqtk_fqchk_se=expand(
            "results/QC/Seqtk/{sample.sample_id}.tsv",
            sample=get_single_ended_samples(),
        ),
        seqtk_fqchk_pe=expand(
            "results/QC/Seqtk/{sample.sample_id}.{stream}.tsv",
            sample=get_pair_ended_samples(),
            stream=stream_tuple,
        ),
    output:
        touch("tmp/targets/fair_fastqc_multiqc_target.flag"),
    localrule: True
    threads: 1
    resources:
        mem_mb=128,
        runtime=2,
        tmpdir="tmp",
    log:
        "logs/fair_fastqc_multiqc_target.log",
    benchmark:
        "benchmark/fair_fastqc_multiqc_target.tsv"
